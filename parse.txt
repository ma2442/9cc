program = ( (func | strc | enm) ";" )*
func = declaration "(" (declaration ("," declaration)* )? ")" "{" labeled* "}"
labeled = label ":" labeled | stmt
stmt 
    = (decla_and_assign | "break" | "continue") ";"
    | "{" labeled* "}"
    | "return" (expr) ";" 
    | "if" "(" expr ")" labeled ("else" labeled )? 
    | "switch" "(" expr ")" labeled 
    | "do" labeled "while" "(" expr ")" ";"
    | "while" "(" expr ")" labeled
    | "for" "(" decla_and_assign? ";" expr? ";" expr? ")" labeled
    | "goto" label ";"
    | ...
decla_and_assign = declaration ("=" assign)? | expr
assign  = cond ("=" assign)?
declaration = type symbol ("[" num "]")*
type = (tint | tchar | stc | enm ) "*"* 
tint
    = ("signed" | "unsigned")? ("long" "long" | "long" | "short")? ("int")
    | ("signed" | "unsigned")? ("long" "long" | "long" | "short")  ("int")?
    | ("signed" | "unsigned")  ("long" "long" | "long" | "short")? ("int")?
tchar = ("signed" | "unsigned")? "char"
stc = "struct" (tag)? "{" ( declaration ";" )* "}"
enm = "enum" (tag)? "{" ( symbol ("," symbol)* )? "}"
expr    = compo_assign 
compo_assign 
    = cond ( ( "<<=" | ">>=" | "|=" | "^=" | "&="
              | "+=" | "-=" | "*=" | "/=" | "%=" | "="
    ) compo_assign )?
cond = bool_or "?" (expr) ":" cond
bool_or = bool_and ("||" bool_and)*
bool_and = or ("&&" or)*
or = xor ("|" xor)*
xor = and ("^" and)*
and = equality ("&" equality)*
equality    = relational (("=="|"!=") relational)*
relational  = shift (("<"|">"|"<="|">=") shift)*
shift = add (("<<"|">>") add)*
add = mul (("+"|"-") mul)*
mul = unary (("*"|"/"|"%") unary)*
unary = ("sizeof"|"++"|"--"|"+"|"-"|"*"|"&"|"!"|"~")? unary | regex
regex = primary ( "++" | "--" | "[" expr "]" | (("."|"->") symbol) )*
primary = strlit
    | "(" expr ")"
    | symbol "(" ( expr (","expr)* )? ")" // func call
    | symbol
    | num 
strlit = '"' [^"]* '"'  // 文字列リテラル
label = ident | "case" const | "default"
tag = ident
symbol = ident
ident = ("_" | ALPHA | alpha) ("_" | ALPHA | alpha | ten)*

ALPHA = "A" | "B" | ... | "Z"
alpha = "a" | "b" | ... | "z"
num = ten ten*
ten = "0" | "1" | ... | "9"
