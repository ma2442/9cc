program = func*
func = declaration "(" (declaration ("," declaration)* )? ")" "{" stmt* "}"
stmt    =
    declaration ("=" assign)? ";"
    | expr ";" 
    | "{" stmt* "}"
    | "return" expr ";" 
    | "if" "(" expr ")" stmt ("else" stmt )? 
    | "while" "(" expr ")" stmt
    | "for" "(" expr? ";" expr? ";" expr? ")" stmt
    | ...
declaration = type ident ("[" num "]")*
type = ("int" | "char" | stc ) "*"* 
stc = "struct" ident "{" ( declaration ";" )* "}"
expr    = assign
assign  = bool_or ("=" assign)?
bool_or = bool_and ("||" bool_and)*
bool_and = equality ("&&" equality)*
equality    = relational (("=="|"!=") relational)*
relational  = add (("<"|">"|"<="|">=") add)*
add = mul (("+"|"-") mul)*
mul = unary (("*"|"/"|"%") unary)*
unary = ("sizeof"|"++"|"--"|"+"|"-"|"*"|"&"|"!")? unary | regex
regex = primary ( "++" | "--" | "[" expr "]" | (("."|"->") ident) )*
primary = strlit
    | "(" expr ")"
    | ident "(" ( expr (","expr)* )? ")" // func call
    | ident
    | num 
strlit = '"' [^"]* '"'  // 文字列リテラル
ident = ("_" | ALPHA | alpha) ("_" | ALPHA | alpha | ten)*

ALPHA = "A" | "B" | ... | "Z"
alpha = "a" | "b" | ... | "z"
num = ten ten*
ten = "0" | "1" | ... | "9"
